plugins {
    id 'net.researchgate.release' version '2.8.0'
    id 'com.jfrog.bintray' version '1.8.4'
}

apply plugin: 'java'
apply from: 'release-lib.gradle'
apply from: 'quality.gradle'

sourceCompatibility = 1.6
targetCompatibility = 1.6

project.ext {
    releaseMode = project.hasProperty('releaseMode')
}

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://jitpack.io" }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:client-java:5.0.0-BETA-1'
        compile 'com.epam.reportportal:commons-model:5.0.0-BETA-7'
    } else {
        compile 'com.github.reportportal:client-java:1173ab3'
        compile 'com.github.reportportal:commons-model:c9e2d05'
    }
    compile 'io.cucumber:cucumber-java:4.5.4'
}

wrapper {
    gradleVersion = '5.4.1'
}

publishing.publications.all {
    pom.withXml {
        asNode().dependencies.'*'.findAll() {
            it.scope.text() == 'runtime' && (project.configurations.compile.allDependencies.find { dep ->
                dep.name == it.artifactId.text() && dep.hasProperty('compileScope') && dep.compileScope
            })
        }.each {
            it.scope*.value = 'compile'
        }
    }
}
